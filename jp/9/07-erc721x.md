---
title: ERC721 NFTとの後方互換性
actions: ["答え合わせ", "ヒント"]
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "ZombieCard.sol": |
        pragma solidity ^0.4.25;

        import "./ERC721XToken.sol";
        import "./Ownable.sol";

        contract ZombieCard is ERC721XToken {

            mapping(uint => uint) internal tokenIdToIndividualSupply;
            // 1. マッピングとuintをここで宣言するのだ

            event TokenAwarded(uint indexed tokenId, address claimer, uint amount);

            function name() external view returns (string) {
                return "ZombieCard";
            }

            function symbol() external view returns (string) {
                return "ZCX";
            }

            function individualSupply(uint _tokenId) public view returns (uint) {
                return tokenIdToIndividualSupply(_tokenId);
            }

            function mintToken(uint _tokenId, uint _supply) public onlyOwner {
                require(!exists(_tokenId), "Error: Tried to mint duplicate token id");
                _mint(_tokenId, msg.sender, _supply);
                tokenIdToIndividualSupply[_tokenId] = _supply;
            }

            function awardToken(uint _tokenId, address _to, uint _amount) public onlyOwner {
                require(exists(_tokenId), "TokenID has not been minted");
                if (individualSupply(_tokenId) > 0) {
                    require(_amount <= balanceOf(_from, _tokenId), "Quantity greater than remaining cards");
                    _updateTokenBalance(msg.sender, _tokenId, _amount, ObjectLib.Operations.SUB);
                }
                _updateTokenBalance(_to, _tokenId, _amount, ObjectLib.Operations.ADD);
                emit TokenAwarded(_tokenId, _to, _amount);
            }

            // 2. 関数をここで定義せよ

        }
      "ERC721XToken.sol": |

        // Full implementation with all included files at https://github.com/loomnetwork/erc721x

        pragma solidity ^0.4.25;

        import "./../../Interfaces/ERC721X.sol";

        import "./../../Interfaces/ERC721XReceiver.sol";
        import "./ERC721XTokenNFT.sol";

        import "openzeppelin-solidity/contracts/AddressUtils.sol";
        import "./../../Libraries/ObjectsLib.sol";


        // Additional features over NFT token that is compatible with batch transfers
        contract ERC721XToken is ERC721X, ERC721XTokenNFT {

            using ObjectLib for ObjectLib.Operations;
            using AddressUtils for address;

            bytes4 internal constant ERC721X_RECEIVED = 0x660b3370;
            bytes4 internal constant ERC721X_BATCH_RECEIVE_SIG = 0xe9e5be6a;

            event BatchTransfer(address from, address to, uint256[] tokenTypes, uint256[] amounts);


            modifier isOperatorOrOwner(address _from) {
                require((msg.sender == _from) || operators[_from][msg.sender], "msg.sender is neither _from nor operator");
                _;
            }

            function implementsERC721X() public pure returns (bool) {
                return true;
            }

            /**
             * @dev transfer objects from different tokenIds to specified address
             * @param _from The address to BatchTransfer objects from.
             * @param _to The address to batchTransfer objects to.
             * @param _tokenIds Array of tokenIds to update balance of
             * @param _amounts Array of amount of object per type to be transferred.
             * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
             */
            function _batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts)
                internal
                isOperatorOrOwner(_from)
            {

                // Requirements
                require(_tokenIds.length == _amounts.length, "Inconsistent array length between args");
                require(_to != address(0), "Invalid recipient");

                if (tokenType[_tokenIds[0]] == NFT) {
                    tokenOwner[_tokenIds[0]] = _to;
                    emit Transfer(_from, _to, _tokenIds[0]);
                }

                // Load first bin and index where the object balance exists
                (uint256 bin, uint256 index) = ObjectLib.getTokenBinIndex(_tokenIds[0]);

                // Balance for current bin in memory (initialized with first transfer)
                // Written with bad library syntax instead of as below to bypass stack limit error
                uint256 balFrom = ObjectLib.updateTokenBalance(
                    packedTokenBalance[_from][bin], index, _amounts[0], ObjectLib.Operations.SUB
                );
                uint256 balTo = ObjectLib.updateTokenBalance(
                    packedTokenBalance[_to][bin], index, _amounts[0], ObjectLib.Operations.ADD
                );

                // Number of transfers to execute
                uint256 nTransfer = _tokenIds.length;

                // Last bin updated
                uint256 lastBin = bin;

                for (uint256 i = 1; i < nTransfer; i++) {
                    // If we're transferring an NFT we additionally should update the tokenOwner and emit the corresponding event
                    if (tokenType[_tokenIds[i]] == NFT) {
                        tokenOwner[_tokenIds[i]] = _to;
                        emit Transfer(_from, _to, _tokenIds[i]);
                    }
                    (bin, index) = _tokenIds[i].getTokenBinIndex();

                    // If new bin
                    if (bin != lastBin) {
                        // Update storage balance of previous bin
                        packedTokenBalance[_from][lastBin] = balFrom;
                        packedTokenBalance[_to][lastBin] = balTo;

                        // Load current bin balance in memory
                        balFrom = packedTokenBalance[_from][bin];
                        balTo = packedTokenBalance[_to][bin];

                        // Bin will be the most recent bin
                        lastBin = bin;
                    }

                    // Update memory balance
                    balFrom = balFrom.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.SUB);
                    balTo = balTo.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.ADD);
                }

                // Update storage of the last bin visited
                packedTokenBalance[_from][bin] = balFrom;
                packedTokenBalance[_to][bin] = balTo;

                // Emit batchTransfer event
                emit BatchTransfer(_from, _to, _tokenIds, _amounts);
            }

            function batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts) public {
                // Batch Transfering
                _batchTransferFrom(_from, _to, _tokenIds, _amounts);
            }

            /**
             * @dev transfer objects from different tokenIds to specified address
             * @param _from The address to BatchTransfer objects from.
             * @param _to The address to batchTransfer objects to.
             * @param _tokenIds Array of tokenIds to update balance of
             * @param _amounts Array of amount of object per type to be transferred.
             * @param _data Data to pass to onERC721XReceived() function if recipient is contract
             * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
             */
            function safeBatchTransferFrom(
                address _from,
                address _to,
                uint256[] _tokenIds,
                uint256[] _amounts,
                bytes _data
            )
                public
            {

                // Batch Transfering
                _batchTransferFrom(_from, _to, _tokenIds, _amounts);

                // Pass data if recipient is contract
                if (_to.isContract()) {
                    bytes4 retval = ERC721XReceiver(_to).onERC721XBatchReceived(
                        msg.sender, _from, _tokenIds, _amounts, _data
                    );
                    require(retval == ERC721X_BATCH_RECEIVE_SIG);
                }
            }

            function transfer(address _to, uint256 _tokenId, uint256 _amount) public {
                _transferFrom(msg.sender, _to, _tokenId, _amount);
            }

            function transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                _transferFrom(_from, _to, _tokenId, _amount);
            }

            function _transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount)
                internal
                isOperatorOrOwner(_from)
            {
                require(tokenType[_tokenId] == FT);
                require(_amount <= balanceOf(_from, _tokenId), "Quantity greater than from balance");
                require(_to != address(0), "Invalid to address");

                _updateTokenBalance(_from, _tokenId, _amount, ObjectLib.Operations.SUB);
                _updateTokenBalance(_to, _tokenId, _amount, ObjectLib.Operations.ADD);
                emit TransferWithQuantity(_from, _to, _tokenId, _amount);
            }

            function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                safeTransferFrom(_from, _to, _tokenId, _amount, "");
            }

            function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount, bytes _data) public {
                _transferFrom(_from, _to, _tokenId, _amount);
                require(
                    checkAndCallSafeTransfer(_from, _to, _tokenId, _amount, _data),
                    "Sent to a contract which is not an ERC721X receiver"
                );
            }

            function _mint(uint256 _tokenId, address _to, uint256 _supply) internal {
                // If the token doesn't exist, add it to the tokens array
                if (!exists(_tokenId)) {
                    tokenType[_tokenId] = FT;
                    allTokens.push(_tokenId);
                } else {
                    // if the token exists, it must be a FT
                    require(tokenType[_tokenId] == FT, "Not a FT");
                }

                _updateTokenBalance(_to, _tokenId, _supply, ObjectLib.Operations.ADD);
                emit TransferWithQuantity(address(this), _to, _tokenId, _supply);
            }


            function checkAndCallSafeTransfer(
                address _from,
                address _to,
                uint256 _tokenId,
                uint256 _amount,
                bytes _data
            )
                internal
                returns (bool)
            {
                if (!_to.isContract()) {
                    return true;
                }

                bytes4 retval = ERC721XReceiver(_to).onERC721XReceived(
                    msg.sender, _from, _tokenId, _amount, _data);
                return(retval == ERC721X_RECEIVED);
            }

        }
    answer: >
      pragma solidity ^0.4.25;

      import "./ERC721XToken.sol"; import "./Ownable.sol";

      contract ZombieCard is ERC721XToken {

          mapping(uint => uint) internal tokenIdToIndividualSupply;
          mapping(uint => uint) internal nftTokenIdToMouldId;
          uint nftTokenIdIndex = 1000000;

          event TokenAwarded(uint indexed tokenId, address claimer, uint
      amount);

          function name() external view returns (string) {
              return "ZombieCard";
          }

          function symbol() external view returns (string) {
              return "ZCX";
          }

          function individualSupply(uint _tokenId) public view returns (uint) {
              return tokenIdToIndividualSupply(_tokenId);
          }

          function mintToken(uint _tokenId, uint _supply) public onlyOwner {
              require(!exists(_tokenId), "Error: Tried to mint duplicate token
      id");
              _mint(_tokenId, msg.sender, _supply);
              tokenIdToIndividualSupply[_tokenId] = _supply;
          }

          function awardToken(uint _tokenId, address _to, uint _amount) public
      onlyOwner {
              require(exists(_tokenId), "TokenID has not been minted");
              if (individualSupply(_tokenId) > 0) {
                  require(_amount <= balanceOf(_from, _tokenId), "Quantity
      greater than remaining cards");
                  _updateTokenBalance(msg.sender, _tokenId, _amount,
      ObjectLib.Operations.SUB);
              }
              _updateTokenBalance(_to, _tokenId, _amount,
      ObjectLib.Operations.ADD);
              emit TokenAwarded(_tokenId, _to, _amount);
          }

          function convertToNFT(uint _tokenId, uint _amount) public {
            require(tokenType[_tokenId] == FT);
            require(_amount <= balanceOf(msg.sender, _tokenId), "You do not own
      enough tokens");
          }

      }
---

ERC721 の代わりに ERC721x を使うと、かなり大きなメリットがある —  トークンのバッ
チ生成やバッチトランスファー、さらに同一コントラクトでの複数トークンクラスといっ
たことが可能になるのだ。

Zombie Battleground では、バッチ生成やバッチトランスファーが可能であることで、初
期バッカーの皆さんにカードを届ける際のガス代が 1000 分の１となった 😮

こういうわけでコスト削減のメリットを享受するため、これからより多くのウォレットや
マーケットプレイスが従来の ERC721 から ERC721x へと切り替えていくのではと思われ
る。

だがそれまでの間、我々は ERC721x を ERC721 と完全な後方互換性を持たせておいたか
ら、新基準をサポートしていない既存の ERC721 ウォレットやマーケットプレイスでユー
ザーがトークンを使うことが可能となっているのだ。

そしてこれを実行するために、コントラクト内に関数をいくつか実装して、トークンをフ
ァンジブルから非ファンジブルに、そしてその逆に変換できるようにしていくぞ。

## FT から NFT への変換

ERC721x は同一コントラクトで FT(代替可能トークン)と NFT(不代替トークン)の両方を
サポートするために作られたものだ。

この実装における主な違いは、NFT はそれぞれにユニークな tokenId を持っているが FT
にはない、という点だ。なぜなら FT は互いに代替可能だからな(さらにそれぞれにユニ
ークなトークン ID を与えると、バッチトランスファーを行いたいときなどにガス代が高
くなってしまうのだ)。

だから ERC721x トークンを従来のウォレットと互換にするために、FT から NFT に変換
する方法 —  つまり変換プロセスにおいてユニークなトークン ID を生成することが必要
になるのだ。

Loom PlasmaChain にある Zombie Battleground の実装では、ユーザーが PlasmaChain
から Ethereum メインネットへとトークンを移転するときにこの変換を行なっている。

だがこの CryptoZombies のレッスンの実装では、簡単に変換を行うメソッドを書くだけ
にしよう —  従来のサービスで機能するようにトークンを ERC721 へと変換する必要があ
れば、ユーザーは後から手動でメソッドを呼び出すことができるからな。

# さあテストだ

はじめにコントラクト内でいくつかの新たな変数を宣言し、NFT についての情報を保存し
なくてはならない。

1. `nftTokenIdToMouldId`という`uint => uint`の`internal`な`mapping`を宣言せよ。

NFT に新規 tokenId を生成するから、元々の FT の tokenId が何であったか覚えておく
手段が必要になる。我々はこの ID をカードの「型」と呼ぶことにしよう —  鉄製の型で
カードの同一コピーを新たにプリントするところを想像してみるのだ。

2. 新たな NFT トークン ID の生成方法が必要だ。そこでマッピングの下
   に`nftTokenIdIndex`という`uint`を宣言し、これを`1000000`に等しく設定せよ。

NFT トークン ID がどの FT トークン ID とも衝突しないことを確認したいから、NFT の
ID は 100 万からスタートして、それよりも小さい ID は FT のために取っておくのだ。
我々のゲームに 100 万枚以上のカードができるとは考えにくいからな。

3. コントラクトの下の方で、`convertToNFT`という名の`function`を宣言せよ。これは
   引数として`uint _tokenId`と`uint _amount`を受けとり、さらに`public`とすること
   。

4. 関数の始めに、２つの`require`ステートメントが必要だ。まずは変換しようとしてい
   るこのトークンが NFT ではなく FT であることを要件としたい。これは
   ERC721XToken.sol にあるコードで実行可能だ:
   `require(tokenType[_tokenId] == FT);`

5. 次に、`msg.sender`が`_amount`以上の`_tokenId`残高を持っていることを確認したい
   。これは`awardToken`で使用したのと同じ`require`ステートメントになるが、今回は
   エラーメッセージ`"You do not own enough tokens"`を出力するようにするのだ。

   > 注: CryptoZombies のアンサーチェッカーは簡易的なものであるため、`_amount`が
   > 最初に来る場合のみ正解となります。

この関数実装の残りは、次のチャプターで完成させていくからな。
