---
title: åŸºæœ¬çš„é–¢æ•°ã®ã‚ªãƒ¼ãƒãƒ¼ãƒ©ã‚¤ãƒ‰
actions: ["ç­”ãˆåˆã‚ã›", "ãƒ’ãƒ³ãƒˆ"]
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "ZombieCard.sol": |
        pragma solidity ^0.4.25;

        import "./ERC721XToken.sol";

        contract ZombieCard is ERC721XToken {

          // ã“ã“ã‹ã‚‰å§‹ã‚ã‚‹ã®ã

        }
      "ERC721XToken.sol": |

        // Full implementation with all included files at https://github.com/loomnetwork/erc721x

        pragma solidity ^0.4.25;

        import "./../../Interfaces/ERC721X.sol";

        import "./../../Interfaces/ERC721XReceiver.sol";
        import "./ERC721XTokenNFT.sol";

        import "openzeppelin-solidity/contracts/AddressUtils.sol";
        import "./../../Libraries/ObjectsLib.sol";


        // Additional features over NFT token that is compatible with batch transfers
        contract ERC721XToken is ERC721X, ERC721XTokenNFT {

            using ObjectLib for ObjectLib.Operations;
            using AddressUtils for address;

            bytes4 internal constant ERC721X_RECEIVED = 0x660b3370;
            bytes4 internal constant ERC721X_BATCH_RECEIVE_SIG = 0xe9e5be6a;

            event BatchTransfer(address from, address to, uint256[] tokenTypes, uint256[] amounts);


            modifier isOperatorOrOwner(address _from) {
                require((msg.sender == _from) || operators[_from][msg.sender], "msg.sender is neither _from nor operator");
                _;
            }

            function implementsERC721X() public pure returns (bool) {
                return true;
            }

            /**
             * @dev transfer objects from different tokenIds to specified address
             * @param _from The address to BatchTransfer objects from.
             * @param _to The address to batchTransfer objects to.
             * @param _tokenIds Array of tokenIds to update balance of
             * @param _amounts Array of amount of object per type to be transferred.
             * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
             */
            function _batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts)
                internal
                isOperatorOrOwner(_from)
            {

                // Requirements
                require(_tokenIds.length == _amounts.length, "Inconsistent array length between args");
                require(_to != address(0), "Invalid recipient");

                if (tokenType[_tokenIds[0]] == NFT) {
                    tokenOwner[_tokenIds[0]] = _to;
                    emit Transfer(_from, _to, _tokenIds[0]);
                }

                // Load first bin and index where the object balance exists
                (uint256 bin, uint256 index) = ObjectLib.getTokenBinIndex(_tokenIds[0]);

                // Balance for current bin in memory (initialized with first transfer)
                // Written with bad library syntax instead of as below to bypass stack limit error
                uint256 balFrom = ObjectLib.updateTokenBalance(
                    packedTokenBalance[_from][bin], index, _amounts[0], ObjectLib.Operations.SUB
                );
                uint256 balTo = ObjectLib.updateTokenBalance(
                    packedTokenBalance[_to][bin], index, _amounts[0], ObjectLib.Operations.ADD
                );

                // Number of transfers to execute
                uint256 nTransfer = _tokenIds.length;

                // Last bin updated
                uint256 lastBin = bin;

                for (uint256 i = 1; i < nTransfer; i++) {
                    // If we're transferring an NFT we additionally should update the tokenOwner and emit the corresponding event
                    if (tokenType[_tokenIds[i]] == NFT) {
                        tokenOwner[_tokenIds[i]] = _to;
                        emit Transfer(_from, _to, _tokenIds[i]);
                    }
                    (bin, index) = _tokenIds[i].getTokenBinIndex();

                    // If new bin
                    if (bin != lastBin) {
                        // Update storage balance of previous bin
                        packedTokenBalance[_from][lastBin] = balFrom;
                        packedTokenBalance[_to][lastBin] = balTo;

                        // Load current bin balance in memory
                        balFrom = packedTokenBalance[_from][bin];
                        balTo = packedTokenBalance[_to][bin];

                        // Bin will be the most recent bin
                        lastBin = bin;
                    }

                    // Update memory balance
                    balFrom = balFrom.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.SUB);
                    balTo = balTo.updateTokenBalance(index, _amounts[i], ObjectLib.Operations.ADD);
                }

                // Update storage of the last bin visited
                packedTokenBalance[_from][bin] = balFrom;
                packedTokenBalance[_to][bin] = balTo;

                // Emit batchTransfer event
                emit BatchTransfer(_from, _to, _tokenIds, _amounts);
            }

            function batchTransferFrom(address _from, address _to, uint256[] _tokenIds, uint256[] _amounts) public {
                // Batch Transfering
                _batchTransferFrom(_from, _to, _tokenIds, _amounts);
            }

            /**
             * @dev transfer objects from different tokenIds to specified address
             * @param _from The address to BatchTransfer objects from.
             * @param _to The address to batchTransfer objects to.
             * @param _tokenIds Array of tokenIds to update balance of
             * @param _amounts Array of amount of object per type to be transferred.
             * @param _data Data to pass to onERC721XReceived() function if recipient is contract
             * Note:  Arrays should be sorted so that all tokenIds in a same bin are adjacent (more efficient).
             */
            function safeBatchTransferFrom(
                address _from,
                address _to,
                uint256[] _tokenIds,
                uint256[] _amounts,
                bytes _data
            )
                public
            {

                // Batch Transfering
                _batchTransferFrom(_from, _to, _tokenIds, _amounts);

                // Pass data if recipient is contract
                if (_to.isContract()) {
                    bytes4 retval = ERC721XReceiver(_to).onERC721XBatchReceived(
                        msg.sender, _from, _tokenIds, _amounts, _data
                    );
                    require(retval == ERC721X_BATCH_RECEIVE_SIG);
                }
            }

            function transfer(address _to, uint256 _tokenId, uint256 _amount) public {
                _transferFrom(msg.sender, _to, _tokenId, _amount);
            }

            function transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                _transferFrom(_from, _to, _tokenId, _amount);
            }

            function _transferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount)
                internal
                isOperatorOrOwner(_from)
            {
                require(tokenType[_tokenId] == FT);
                require(_amount <= balanceOf(_from, _tokenId), "Quantity greater than from balance");
                require(_to != address(0), "Invalid to address");

                _updateTokenBalance(_from, _tokenId, _amount, ObjectLib.Operations.SUB);
                _updateTokenBalance(_to, _tokenId, _amount, ObjectLib.Operations.ADD);
                emit TransferWithQuantity(_from, _to, _tokenId, _amount);
            }

            function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount) public {
                safeTransferFrom(_from, _to, _tokenId, _amount, "");
            }

            function safeTransferFrom(address _from, address _to, uint256 _tokenId, uint256 _amount, bytes _data) public {
                _transferFrom(_from, _to, _tokenId, _amount);
                require(
                    checkAndCallSafeTransfer(_from, _to, _tokenId, _amount, _data),
                    "Sent to a contract which is not an ERC721X receiver"
                );
            }

            function _mint(uint256 _tokenId, address _to, uint256 _supply) internal {
                // If the token doesn't exist, add it to the tokens array
                if (!exists(_tokenId)) {
                    tokenType[_tokenId] = FT;
                    allTokens.push(_tokenId);
                } else {
                    // if the token exists, it must be a FT
                    require(tokenType[_tokenId] == FT, "Not a FT");
                }

                _updateTokenBalance(_to, _tokenId, _supply, ObjectLib.Operations.ADD);
                emit TransferWithQuantity(address(this), _to, _tokenId, _supply);
            }


            function checkAndCallSafeTransfer(
                address _from,
                address _to,
                uint256 _tokenId,
                uint256 _amount,
                bytes _data
            )
                internal
                returns (bool)
            {
                if (!_to.isContract()) {
                    return true;
                }

                bytes4 retval = ERC721XReceiver(_to).onERC721XReceived(
                    msg.sender, _from, _tokenId, _amount, _data);
                return(retval == ERC721X_RECEIVED);
            }

        }
    answer: >
      pragma solidity ^0.4.25;

      import "./ERC721XToken.sol";

      contract ZombieCard is ERC721XToken {

          function name() external view returns (string) {
              return "ZombieCard";
          }

          function symbol() external view returns (string) {
              return "ZCX";
          }

      }
---

`ERC721XToken.sol`ã‚’ç¶™æ‰¿ã—ã¦ã„ã‚‹ã‹ã‚‰ã€`transfer`ã‚„`balanceOf`ã¨ã„ã£ãŸå¿…è¦ãªåŸºæœ¬
ã®é–¢æ•°ã¯ã™ã§ã«å®šç¾©æ¸ˆã¿ã«ãªã£ã¦ã„ã‚‹ã€‚

ERC721x ã®ã‚¤ãƒ³ã‚¿ãƒ¼ãƒ•ã‚§ãƒ¼ã‚¹ã¯ã“ã‚“ãªæ„Ÿã˜ã«ãªã£ã¦ã„ã‚‹ã€‚èˆˆå‘³ãŒã‚ã‚‹ãªã‚‰è¦‹ã¦ã¿ã‚ˆã†:

```
contract ERC721X {
  function implementsERC721X() public pure returns (bool);
  function ownerOf(uint256 _tokenId) public view returns (address _owner);
  function balanceOf(address owner) public view returns (uint256);
  function balanceOf(address owner, uint256 tokenId) public view returns (uint256);
  function tokensOwned(address owner) public view returns (uint256[], uint256[]);

  function transfer(address to, uint256 tokenId, uint256 quantity) public;
  function transferFrom(address from, address to, uint256 tokenId, uint256 quantity) public;

  // Fungible Safe Transfer From
  function safeTransferFrom(address from, address to, uint256 tokenId, uint256 _amount) public;
  function safeTransferFrom(address from, address to, uint256 tokenId, uint256 _amount, bytes data) public;

  // Batch Safe Transfer From
  function safeBatchTransferFrom(address _from, address _to, uint256[] tokenIds, uint256[] _amounts, bytes _data) public;

  function name() external view returns (string);
  function symbol() external view returns (string);

  // Required Events
  event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);
  event TransferWithQuantity(address indexed from, address indexed to, uint256 indexed tokenId, uint256 quantity);
  event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);
  event BatchTransfer(address indexed from, address indexed to, uint256[] tokenTypes, uint256[] amounts);
}
```

ERC721x ã§ã‚ã‚‹ã¨å®£è¨€ã—ã¦ã„ã‚‹ãƒˆãƒ¼ã‚¯ãƒ³ã¯å…¨ã¦ã€ã“ã‚Œã‚‰å…¨ãƒ¡ã‚½ãƒƒãƒ‰ã‚’å®Ÿè£…ã—ã¦ã„ã‚‹ã“ã¨ãŒ
ä¿è¨¼ã•ã‚Œã¦ã„ã‚‹ã€‚ERC721x ãƒˆãƒ¼ã‚¯ãƒ³å‘ã‘ã«ä½œã‚‰ã‚ŒãŸã‚¦ã‚©ãƒ¬ãƒƒãƒˆã‚„ãƒãƒ¼ã‚±ãƒƒãƒˆãƒ—ãƒ¬ã‚¤ã‚¹ã¯ã€
ãã‚Œã‚‰ãƒ¡ã‚½ãƒƒãƒ‰ã®å‘¼ã³å‡ºã—ãŒå¯èƒ½ãªã®ã (ã•ã‚‰ã«ã™ã§ã«èª¬æ˜ã—ãŸã‚ˆã†ã«ã€ã“ã‚Œã¯ ERC721
ãƒˆãƒ¼ã‚¯ãƒ³åŸºæº–ã¨å®Œå…¨ãªå¾Œæ–¹äº’æ›æ€§ã‚’æŒã£ã¦ã„ã‚‹)ã€‚

`ERC721XToken.sol`ã‚’ã‚¤ãƒ³ãƒãƒ¼ãƒˆã—ã¦ã„ã‚‹ã‹ã‚‰ã€ã“ã‚Œã‚‰ã™ã¹ã¦ã®é–¢æ•°ã€ã¤ã¾ã‚Šãƒˆãƒ¼ã‚¯ãƒ³ã®
é€ä¿¡ã‚„ãƒ¦ãƒ¼ã‚¶ãƒ¼é–“ã®ç§»è»¢ã‚’æ‰¿èªã™ã‚‹ãƒ­ã‚¸ãƒƒã‚¯ã®å…¨ã¦ã¯ã™ã§ã«å®Ÿè£…æ¸ˆã¿ã¨ãªã£ã¦ã„ã‚‹ã€‚

ã“ã®ãƒ¬ãƒƒã‚¹ãƒ³ã§ã¯ã€ã©ã‚“ãªã‚«ãƒ¼ãƒ‰ãŒã‚²ãƒ¼ãƒ ã«å­˜åœ¨ã—ã€ãã‚Œã‚‰ãŒã©ã®ã‚ˆã†ã«ç”Ÿæˆã•ã‚Œãƒ¦ãƒ¼ã‚¶
ãƒ¼ã«é…ã‚‰ã‚Œã‚‹ã®ã‹ã€ã¨ã„ã£ãŸã“ã¨ã«ã¤ã„ã¦ã®ãƒ«ãƒ¼ãƒ«ã®è¨­å®šã«ã¤ã„ã¦ãƒ•ã‚©ãƒ¼ã‚«ã‚¹ã—ã¦è¡Œãã
ã€‚

ã ãŒã¾ãšã¯ã€ä½œæˆã—ãŸã‚²ãƒ¼ãƒ ã‚¢ã‚¤ãƒ†ãƒ ã§ã‚ªãƒ¼ãƒãƒ¼ãƒ©ã‚¤ãƒ‰ãŒå¿…è¦ã«ãªã‚‹ã§ã‚ã‚ã†æœ€ã‚‚åŸºæœ¬çš„
ãªé–¢æ•°ã‹ã‚‰å§‹ã‚ã¦ã„ã“ã†ã€‚

### `name`

ã‚¦ã‚©ãƒ¬ãƒƒãƒˆã‚„å–å¼•æ‰€ãŒã©ã®ã‚²ãƒ¼ãƒ ç”¨ã®ã‚¢ã‚¤ãƒ†ãƒ ã‹åˆ¤ã‚‹ã‚ˆã†ã«ã€ERC721x ãƒˆãƒ¼ã‚¯ãƒ³ã«åå‰ã‚’
ã¤ã‘ã‚‹å¿…è¦ãŒã‚ã‚‹ã ã‚ã†ã€‚

```
function name() external view returns (string) {
  return "ExampleCard";
}
```

ã“ã‚Œã¯ãƒˆãƒ¼ã‚¯ãƒ³åã‚’è¿”ã™ã ã‘ã®ã‚·ãƒ³ãƒ—ãƒ«ãªé–¢æ•°ã ã€‚

### `symbol`

ãƒˆãƒ¼ã‚¯ãƒ³ã¯ã€ã‚¦ã‚©ãƒ¬ãƒƒãƒˆã‚„ãƒãƒ¼ã‚±ãƒƒãƒˆãƒ—ãƒ¬ã‚¤ã‚¹ã€å–å¼•æ‰€ã§ä½¿ã†ã“ã¨ã®ã§ãã‚‹ã‚·ãƒ³ãƒœãƒ«ã‚‚æŒ
ã£ã¦ã„ãªãã¦ã¯ãªã‚‰ãªã„ã€‚

```
function symbol() external view returns (string) {
  return "ECX";
}
```

ã“ã®é–¢æ•°ã‚‚ã‚·ãƒ³ãƒ—ãƒ«ãªã‚‚ã®ã§ã€ãƒ†ã‚£ãƒƒã‚«ãƒ¼ã‚·ãƒ³ãƒœãƒ«ã‚’è¿”å´ã™ã‚‹ã ã‘ã®ã‚‚ã®ã ã€‚ãƒ†ã‚£ãƒƒã‚«ãƒ¼
ã‚·ãƒ³ãƒœãƒ«ã¯ï¼“æ–‡å­—ã§ã€å…¨ã¦å¤§æ–‡å­—ã¨ãªã£ã¦ã„ã‚‹ã€‚

# ã•ã‚ãƒ†ã‚¹ãƒˆã

æ—©é€Ÿã“ã®ï¼’ã¤ã®é–¢æ•°ã‚’ ZombieCard ã§å®Ÿè£…ã—ã¦ã¿ã‚‹ã®ã ã€‚

1. `name`ã¨ã„ã†é–¢æ•°ã‚’ä½œæˆã›ã‚ˆã€‚ã“ã‚Œã¯`external view`é–¢æ•°ã§ã€"ZombieCard"ã¨ã„
   ã†`string`ã‚’è¿”å´ã™ã‚‹ã‚ˆã†ã«ã™ã‚‹ã“ã¨ã€‚

2. `symbol`ã¨ã„ã†åã®é–¢æ•°ã‚’ä½œæˆã›ã‚ˆã€‚`external view`é–¢æ•°ã§ã€"ZCX"ã¨ã„
   ã†`string`ã‚’è¿”å´ã™ã‚‹ã‚ˆã†ã«ã™ã‚‹ã“ã¨ã€‚
