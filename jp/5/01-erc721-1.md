---
title: イーサリアム上トークン
actions: ["答え合わせ", "ヒント"]
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        // ここから始めるのだ
      "zombieattack.sol": |
        pragma solidity ^0.4.19;

        import "./zombiehelper.sol";

        contract ZombieBattle is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(now, msg.sender, randNonce)) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }
      "zombiehelper.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            owner.transfer(this.balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }
      "zombiefeeding.sol": |
        pragma solidity ^0.4.19;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(_species) == keccak256("kitty")) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }
      "zombiefactory.sol": |
        pragma solidity ^0.4.19;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }
      "ownable.sol": |
        /**
         * @title Ownable
         * @dev The Ownable contract has an owner address, and provides basic authorization control
         * functions, this simplifies the implementation of "user permissions".
         */
        contract Ownable {
          address public owner;

          event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

          /**
           * @dev The Ownable constructor sets the original `owner` of the contract to the sender
           * account.
           */
          function Ownable() public {
            owner = msg.sender;
          }


          /**
           * @dev Throws if called by any account other than the owner.
           */
          modifier onlyOwner() {
            require(msg.sender == owner);
            _;
          }


          /**
           * @dev Allows the current owner to transfer control of the contract to a newOwner.
           * @param newOwner The address to transfer ownership to.
           */
          function transferOwnership(address newOwner) public onlyOwner {
            require(newOwner != address(0));
            OwnershipTransferred(owner, newOwner);
            owner = newOwner;
          }

        }
    answer: |
      pragma solidity ^0.4.19;

      import "./zombieattack.sol";

      contract ZombieOwnership is ZombieAttack {

      }
---

**_トークン_** について説明していこう。

お主がイーサリアムの界隈に足を踏み入れたことがあるなら、トークン、特に **_ERC20
トークン_** が話題になってるのを恐らく聞いたことがあるだろう。

イーサリアム上の**_トークン_** は、基本的にいくつかの共通ルールに従ったスマート
・コントラクトだ。具体的に言うと、`transfer(address _to, uint256 _value)` や
`balanceOf(address _owner)` といった関数のスタンダードセットを実装しているものだ
。

通常スマートコントラクトは、内部に各アドレスにどれだけの残高があるかを記録する
`mapping(address => uint256) balances`を持っている。

つまり基本的には、トークンとは、誰がトークンをどれくらいを所有しているのかを記録
するコントラクトと、ユーザーが自分のトークンを他のアドレスに送ることができるよう
にする機能のことなのだ。

### なぜトークンが重要なのか?

すべての ERC20 トークンは同じ名前の同じ関数セットを共用しているから、同じ方法で
相互に作用することが可能となっている。

つまり、とある ERC20 トークンとやりとりするアプリケーションを作った場合、他の
ERC20 トークンとやりとりすることも可能なのだ。こうすることでカスタムコーディング
をせずとも、将来もっと多くのトークンをアプリに追加することができるぞ。ただ新しい
トークンのコントラクト・アドレスを入力するだけで、もうアプリは別のトークンを使え
るようになる。

この一例として挙げられるのは取引所であろう。取引所が新たな ERC20 トークンを追加
するのに必要なのは、トークンとやり取りするためのスマート・コントラクトをただ追加
することだ。ユーザーはトークンを取引所のウォレットアドレスに送るようコントラクト
に指示でき、取引所はユーザーが引き出しを要求した場合にトークンを彼らに送り返すよ
うコントラクトに指示することができる。

取引所はこのトランスファー・ロジックを一度実装すれば、新たに ERC20 トークンを追
加したい場合に、新しいコントラクト・アドレスをデータベースに追加するだけで良いと
いうことだ。

### 別のトークン規格

ERC20 トークンは、通貨のような働きをする非常に素晴らしいトークンだ。 ただし、こ
の規格で我らがゾンビ・ゲーム内のゾンビを表すのは非常に不便である。

理由としてまず、ゾンビは通貨のように分けることができないことが挙げられる。例えば
0.237ETH をお主に送ることはできても、0.237 のゾンビを送るだなんて訳がわからない
からな。

次に、全てのゾンビが同じように出来ていないということもその理由だ。例えば、お主の
レベル２のゾンビ"**Steve**"が、わしのレベル 732 のゾンビ"**H4XF13LD MORRIS
💯💯😎💯💯**"と同じものだなんてことは全くない！(_Steve_ よ、わしのゾンビとは程遠
いぞ)

なのでそれとは別に、CryptoZombies のようなクリプト収集物により適したトークン規格
がある。 **_ERC721 トークン_** と呼ばれるものだ。

**_ERC721 トークン_** は、それぞれがユニークであると仮定され、分割出来ないので
**相互に交換可能でない** 。一つの単位ごとの取引のみ可能で、それぞれが特有の Id
を持っている。だからゾンビをトレード可能にするのに完璧に適したものなのだ。

> 注: ERC721 のような標準を使用するメリットとして、オークション及びプレイヤーが
> ゾンビをトレード/販売するやり方を決定するエスクロー・ロジックをコントラクト内
> で実装する必要がなくなるという点があります。仕様に準拠すれば、他の誰かが収集可
> 能な ERC721 クリプト資産の交換プラットフォームを作ることができ、私たちの
> ERC721 ゾンビはそのプラットフォームで使用できるようになります。そのため、独自
> の取引ロジックを展開する代わりにトークン規格を使用するのは明らかにメリットがあ
> ります。

## さあテストだ

次のチャプターで ERC721 の実装をやっていくが、まずはこのレッスン向けにファイル構
成をセットアップしていくぞ。

`ZombieOwnership`というコントラクトに全ての ERC721 メソッドを格納していこう。

1. `pragma`のバージョンをファイルの冒頭で宣言せよ。(シンタックスは前のレッスンで
   やったファイルをチェックすること）

2. このファイルに`zombieattack.sol`を`import`すること。

3. `ZombieOwnership`という新たなコントラクトを宣言し、`ZombieAttack`を継承させよ
   。まだコントラクトの中身は空のままでよい。
